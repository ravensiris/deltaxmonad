#+TITLE: XMonad Config
#+AUTHOR: Raven's Iris
#+PROPERTY: header-args :tangle xmonad.hs

* Imports
#+BEGIN_SRC haskell 
  import           Data.Ratio

  import           XMonad
  import           XMonad.Hooks.DynamicLog
  import           XMonad.Layout.ThreeColumns
  import           XMonad.Util.Run                ( spawnPipe
						  , hPutStrLn
						  )
  import           XMonad.Hooks.EwmhDesktops      ( ewmh )
  import           XMonad.Hooks.ManageDocks
  import           XMonad.Layout.NoBorders        ( noBorders )
  import           XMonad.Layout.Gaps
  import           XMonad.Layout.Spacing          ( smartSpacing )
  import           XMonad.Layout.Fullscreen       ( fullscreenSupport
						  , fullscreenFocus
						  )
  import           XMonad.Util.Cursor
  import qualified Data.Map                      as M
  import           XMonad.Util.EZConfig
  import           XMonad.Actions.Promote
  import           XMonad.Util.NamedScratchpad
  import qualified XMonad.StackSet               as W
#+END_SRC

* Layout
** Gaps
#+BEGIN_SRC haskell 
  myGaps = smartSpacing gap . gaps gapSides
    where
      gap = 12
      gapSides = zip [U,D,L,R] (repeat gap)
#+END_SRC
** DONE Rest
Removed borders, added a fulscreen and avoidStruts(for xmobar) hook.
#+BEGIN_SRC haskell 
  threeColLayout = ThreeColMid 1 (3 % 100) (1 % 2)

  myLayout =
    fullscreenFocus
      $ myGaps
      $ noBorders
      $ avoidStruts
      $ threeColLayout
#+END_SRC

* Keybindings
#+BEGIN_SRC haskell 
  myKeyBindings = (`removeKeysP` blacklist) . (`additionalKeysP` whitelist)
   where
    whitelist =
      [ ("M-<Return>", spawn "alacritty")
      , ("<F2>", spawn "pickwallpaper")
      , ("<XF86AudioNext>", spawn "quodlibet --next")
      , ("<XF86AudioPrev>", spawn "quodlibet --previous")
      , ("<XF86AudioPlay>", spawn "quodlibet --play-pause")
      , ("<F3>", spawn "picom-trans -c 95")
      , ("S-<F3>", spawn "picom-trans -c 100")
      , ("M-p", spawn "dmenu_run -fn \"Victor Mono Nerd Font-24\"")
      ] ++ scratchpadsKeybindings myScratchpads

    blacklist =
      [ "M-S-<Return>" -- Terminal bound to M-<Return>
      , "M-."
      , "M-,"
      ]
#+END_SRC

* Scratchpads

#+BEGIN_SRC haskell 
  centeredFloat w h = customFloating $ W.RationalRect x y w h
      where
	  x = 1%2 - (w / 2)
	  y = 1%2 - (h / 2)

  myScratchpads =
    [ ( NS "htop" "alacritty --title htop -e htop" (title =? "htop") terminalFloat
      , "<F1>"
      )
    , ( NS "quodlibet" "quodlibet" (className =? "Quodlibet") mediaFloat
      , "M-m"
      )
    , ( NS "terminal"
	   "alacritty --title ScratchpadAlacritty"
	   (title =? "ScratchpadAlacritty")
	   terminalFloat
      , "M-x"
      )
    ]
    where
      terminalFloat = centeredFloat (1%2) (3%5)
      mediaFloat = centeredFloat (7%10) (4%5)


  scratchpads = map fst myScratchpads
  scratchpadsKeybindings :: [(NamedScratchpad, String)] -> [(String, X ())]
  scratchpadsKeybindings nskbd = map processNS nskbd
    where processNS (ns, kb) = (kb, namedScratchpadAction scratchpads (name ns))
#+END_SRC

* Bar
** Config
#+BEGIN_SRC conf :tangle xmobar.conf
  Config { font = "xft: VictorMono Nerd Font:pixelsize=26:antialias=true:hinting=true:light,xft:Koruri:pixelsize=26:antialias=true:hinting=true"
	 , additionalFonts = []
	 , bgColor = "#2e3440"
	 , fgColor = "#FFFFFF"
	 , alpha = 255
	 , position = Static { xpos = 0 , ypos = 0, width = 2560, height = 27 }
	 , textOffset = -1
	 , iconOffset = -1
	 , lowerOnStart = True
	 , pickBroadest = False
	 , persistent = False
	 , hideOnStart = False
	 , iconRoot = "."
	 , allDesktops = True
	 , overrideRedirect = True
	 , commands = [
			Run Cpu ["-L","3","-H","50"
				 ,"-t"," <total>%"] 10
		      , Run Memory ["-t"," <usedratio>%"] 10
		      , Run Swap ["-t", "<usedratio>%"] 10
		      , Run Date " %a %b %d %Y   %H:%M:%S" "date" 10
		      , Run StdinReader]
	 , sepChar = "%"
	 , alignSep = "||"
	 , template = "<fc=#FFFFFF,#C2185B> %StdinReader% </fc><fc=#C2185B> </fc> ||\
		   \<fc=#FFFFFF,#C2185B><fc=#C2185B> </fc>  %cpu%  %memory% * %swap%  %date%  </fc>"
	 }

#+END_SRC
** Log hook
#+BEGIN_SRC haskell 
  myLogHook barPipe =
    dynamicLogWithPP $ namedScratchpadFilterOutWorkspacePP $ def
      { ppCurrent         = wrap "[" "]"
      , ppTitle           = shorten 136
      , ppUrgent          = id
      , ppHiddenNoWindows = const ""
      , ppSep             = wrap " " " " $ xmobarColor "#FFFFFF" "#880E4F" "<>"
      , ppWsSep           = " "
      , ppLayout          = const ""
      , ppOrder           = id
      , ppOutput          = hPutStrLn barPipe
      , ppExtras          = []
      }
  myStatusBar = "xmobar -x0 /home/raven/.xmonad/xmobar.conf"
#+END_SRC

* Main
#+BEGIN_SRC haskell 
  myConfig barPipe = myKeyBindings $ def
    { modMask       = mod4Mask
    , terminal      = "alacritty"
    , layoutHook    = myLayout
    , logHook       = myLogHook barPipe
    , workspaces    = ["GEN", "WRK", "SYS"] ++ (map show [4 .. 9])
    , manageHook    = namedScratchpadManageHook scratchpads
		      <+> manageDocks
		      <+> manageHook def
    , startupHook   = setDefaultCursor xC_left_ptr
    , normalBorderColor = "#FFFFFF"
    , focusedBorderColor = "#bbc5ff"
    }

  main :: IO ()
  main = do
    barPipe <- spawnPipe myStatusBar
    xmonad $ fullscreenSupport $ docks $ ewmh $ myConfig barPipe
#+END_SRC
